def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def getGitVersionCmd = 'git describe --tags'
def getGitCommitCountCmd = 'git rev-list HEAD --count'
def gitVersionName = getGitVersionCmd.execute().text.trim().substring(1)
def gitVersionCode = getGitCommitCountCmd.execute().text.trim()

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = gitVersionCode
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = gitVersionName
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

// For release signing.
def keystorePropertiesFile = rootProject.file("key.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "org.ctbeta.xmux.xmux"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            useProguard false
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
            ndk {
                // TODO: Dynamic abiFilters for specific platform.
                if (project.hasProperty('target-platform')) {
                    if (project.property('target-platform') == 'android-arm,android-arm64')
                        abiFilters 'armeabi-v7a', 'arm64-v8a'
                    else if (project.property('target-platform') == 'android-arm')
                        abiFilters 'armeabi-v7a'
                    else if (project.property('target-platform') == 'android-arm64')
                        abiFilters 'arm64-v8a'
                }
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'